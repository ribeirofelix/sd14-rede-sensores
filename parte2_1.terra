@include(/home/terra/TerraNet_v0.1/terra/Terra.m4)
var u8 lastReq = 0;
var u8 pai1 = 0;
var u8 pai2 = 0;
var u8 acumulador1 = 0;
var u8 acumulador2 = 0;
event u8 requestArrived;
event u8 requestSent;
event u8 requestFinished;
/* no raiz */
if nodeId == 11 then
	par do
		usrMsg_t(msgRadio1,1);
		msgRadio1_source = nodeId;
		msgRadio1_target = BROADCAST;
		msgRadio1_d8_1 = 0; /* 0-> request ; 1 -> answer */

		/* envio de requisicoes */
		loop do
			lastReq = lastReq + 1;
			msgRadio1_id = lastReq;
			emit SEND (msgRadio1);
			par/or do
				await SEND_DONE;
			with
				await 5s;
			end 
			emit LED0(3);
			await 1s;
			emit requestSent ();
			await requestFinished;
		end
	with
		usrMsg_t(msgRadio2,1);
		/* tratamento de respostas */
		loop do
			await requestSent;
			par/or do
				loop do
					msgRadio2 = await RECEIVE;
					if msgRadio2_d8_1 == 1 then /* mensagem e uma resposta */
						acumulador1 = acumulador1 + 1;
						acumulador2 = acumulador2 + msgRadio2_d8_3;
						emit LED2(3);
						await 1s;
					end
				end
			with
				await 60s; /* timeout */
			end
			acumulador1 = 0;
			acumulador2 = 0;
			emit requestFinished();
		end
	end
else
	par do
		usrMsg_t(msgRadio2,1);
		loop do
			msgRadio2 = await RECEIVE;
			/* respostas de nos filhos */
			if msgRadio2_d8_1 == 1 then 
				msgRadio2_target = pai1;
				par/or do
					emit SEND (msgRadio2);
					await SEND_DONE;
				with
					await 5s;
					msgRadio2_target = pai2;
					par/or do
						emit SEND (msgRadio2);
						await SEND_DONE;
					with
						await 5s;
					end
				end
				emit LED2(3);
				await 1s;
			end
		end
	with
		usrMsg_t(msgRadio3,1);
		loop do
			msgRadio3 = await RECEIVE;
			/* requisicao */
			if msgRadio3_d8_1 == 0 then 
				/* sendo a primeira requisicao, setamos os pais */
				if pai1 == 0  then
					pai1 = msgRadio3_source;
					emit LED1(1);
				else/if pai2 == 0  then
					pai2 = msgRadio3_source;
				end
			end
			/* envia resposta se nao for duplicada */
			if msgRadio3_id > lastReq and (msgRadio3_source==pai1 or msgRadio3_source==pai2)  then
				par do
					/* so atende novas requisicoes dos pais */
					emit REQ_PHOTO();
					usrMsg_t(msgRadio4,1);
					msgRadio4_source = nodeId;
					msgRadio4_target = pai1;
					msgRadio4_d8_1 = 1; /* resposta */
					msgRadio4_d8_3 = await PHOTO;
					par/or do
						emit SEND (msgRadio4);
						await SEND_DONE;
					with
						await 5s;
						msgRadio4_target = pai2;
						par/or do
							emit SEND (msgRadio4);
							await SEND_DONE;
						with
							await 5s;
						end
					end
					emit LED0(3);
					await 1s;
				with
					msgRadio3_source = nodeId;
					emit SEND (msgRadio3);
					par/or do
						await SEND_DONE;
					with
						await 5s;
					end
				end
			end
		end
	end
end
